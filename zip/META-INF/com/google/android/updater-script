ui_print("#############################################");
ui_print("#        ParrotMod v2.0 - Universal         #");
ui_print("#   By: @parrotgeek1 (www.parrotgeek.com)   #");
ui_print("#############################################");
ui_print("");

ui_print("Checking requirements...");

assert(getprop("ro.product.board") == "grouper" || abort("This device is not a 2012 Nexus 7!"));

run_program("/sbin/busybox", "mount", "/system");

assert(!less_than_int(file_getprop("/system/build.prop","ro.build.version.sdk"),17) && less_than_int(file_getprop("/system/build.prop","ro.build.version.sdk"),24) || abort ("ERROR: ParrotMod currently only supports Android 4.2-6.x, refusing to install."));

package_extract_file("exist.sh", "/tmp/exist.sh");		
set_perm(0, 0, 0777, "/tmp/exist.sh");

run_program("/tmp/exist.sh", "/system/xbin/daemonsu");	
if file_getprop("/tmp/exist.prop","file.exists") == "0" then
	delete("/tmp/exist.sh");
	delete("/tmp/exist.prop");
	abort("ERROR: ParrotMod requires the newest version of SuperSU, refusing to install.");
endif;
	
ui_print("Freeing space...");

if file_getprop("/system/build.prop","ro.build.type") == "user" && file_getprop("/system/build.prop","ro.build.tags") == "release-keys" && is_substring("corp.google.com",file_getprop("/system/build.prop","ro.build.host")) then
	run_program("/sbin/busybox", "find","/system/app/XT9IME","-depth","-xdev","-follow","-delete");
	delete("/system/app/XT9IME.apk");
	delete("/system/lib/libjni_xt9input.so");
	delete("/system/su.d/01magic");
	delete("/system/su.d/02parrotmodstock.sh");
endif;

run_program("/sbin/busybox", "mount", "/data");
run_program("/sbin/busybox", "mount", "/cache");

delete_recursive("/system/etc/parrotmod");
delete_recursive("/system/etc/parrotmodstock");
delete("/data/lastpmver.txt");
delete("/data/lastpmver_univ.txt");
delete("/data/system/parrotmod_last_version");

package_extract_file("space.sh", "/tmp/space.sh");		
set_perm(0, 0, 0777, "/tmp/space.sh");

run_program("/tmp/space.sh", "8");
if file_getprop("/tmp/space.prop","enough.space") == "0" then
	delete("/tmp/exist.sh");
	delete("/tmp/exist.prop");
	delete("/tmp/space.sh");
	delete("/tmp/space.prop");
	abort("ERROR: ParrotMod requires at least 8MB of free space on the system partition, refusing to install. Delete some system apps or use a smaller GApps package.");
endif;
delete("/tmp/space.sh");
delete("/tmp/space.prop");

ui_print("Installing...");
ui_print("");

package_extract_dir("system", "/system");
	
run_program("/tmp/exist.sh", "/system/etc/permissions/android.hardware.bluetooth_le.xml");	
if sha1_check(package_extract_file("bt40/"+file_getprop("/system/build.prop","ro.build.version.sdk")+"/sentinel")) != "" && file_getprop("/tmp/exist.prop","file.exists") == "0" then
	package_extract_dir("bt40/system", "/system");
	package_extract_dir("bt40/"+file_getprop("/system/build.prop","ro.build.version.sdk")+"/system", "/system");
endif;

if sha1_check(package_extract_file("libc/"+file_getprop("/system/build.prop","ro.build.version.sdk")+"/sentinel")) != "" then
	package_extract_dir("libc/system", "/system");
	package_extract_dir("libc/"+file_getprop("/system/build.prop","ro.build.version.sdk")+"/system", "/system");
endif;


run_program("/tmp/exist.sh", "/system/xposed.prop");
if sha1_check(package_extract_file("libart/"+file_getprop("/system/build.prop","ro.build.version.sdk")+"/sentinel")) != "" then
	if file_getprop("/tmp/exist.prop","file.exists") == "1" then
		package_extract_file("libart/system/lib/libart.so", "/system/lib/libart.so.orig");
		ui_print("WARNING: Some ART runtime optimizations are not available when Xposed is installed! Please uninstall it to enable them.");
	else
		package_extract_file("libart/system/lib/libart.so", "/system/lib/libart.so");
	endif;
endif;

delete("/tmp/exist.sh");
delete("/tmp/exist.prop");

set_perm_recursive(0, 0, 0700, 0700, "/system/su.d");
set_perm_recursive(0, 0, 0755, 0755, "/system/etc/parrotmod");
set_perm_recursive(0, 0, 0755, 0755, "/system/lib/hw");
set_perm_recursive(0, 0, 0755, 0755, "/system/addon.d");

if !less_than_int(file_getprop("/system/build.prop","ro.build.version.sdk"),17) then
	package_extract_file("miracast_patch.sh", "/tmp/miracast_patch.sh");		
	set_perm(0, 0, 0777, "/tmp/miracast_patch.sh");		
	run_program("/tmp/miracast_patch.sh", "");		
	delete("/tmp/miracast_patch.sh");
endif;

run_program("/sbin/busybox", "chcon", "-R", "u:object_r:system_file:s0", "/system/etc/parrotmod");
run_program("/sbin/busybox", "chcon", "-R", "u:object_r:system_file:s0", "/system/su.d");

run_program("/system/etc/parrotmod/bprop.sh", "");

run_program("/sbin/busybox", "fstrim", "-v", "/system");
run_program("/sbin/busybox", "umount", "/system");

run_program("/sbin/busybox", "fstrim", "-v", "/data");

run_program("/sbin/busybox", "fstrim", "-v", "/cache");

ui_print("Enjoy ParrotMod!");
